# 07 Výsledný program

```C
#include <SPI.h>
#include <RTClib.h>
#include <LiquidCrystal.h>
#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include <DHT.h>


RTC_DS1307 RTC; 
LiquidCrystal lcd(16, 14, 12, 13, 0, 2);
DHT dht(15, DHT11);
HTTPClient http;
WiFiClient client;


String sensor() 
{
    float h = dht.readHumidity();
    float t = dht.readTemperature();
    String result;

    while(isnan(h)) {
        h = dht.readHumidity();
        delay(100);
    }

    while(isnan(t)) {
        t = dht.readTemperature();
        delay(100);
    }

    result = String(t, 0) + "'C / ";
    result += String(h, 0) + "%";

    Serial.println(result);
    return result;
}


String date() 
{
    DateTime now = RTC.now();
    String result;

    if (now.day() < 10) {
        result = '0';
    }

    result += String(now.day()) + '.';
 
    if (now.month() < 10) {
        result += '0';
    }

    result += String(now.month()) + '.' + String(now.year());

    Serial.println(result);
    return result;
}


String time() 
{
    DateTime now = RTC.now();
    String result;

    if (now.hour() < 10) {
        result = '0';
    }

    result += String(now.hour()) + ':';

    if (now.minute() < 10) {
        result += '0';
    }

    result += String(now.minute()) + ':';

    if (now.second() < 10) {
        result += '0';
    }

    result += String(now.second());

    Serial.println(result);
    return result;
}


String weather() 
{
    String data;
    String result;

    http.begin(client, "http://pavol.webtip.sk/weather.php");
    http.GET();

    data = http.getString();

    result = data.substring(data.indexOf('#') + 1);
    http.end();

    Serial.println(result);
    return result;
}


String today() 
{
    DateTime now = RTC.now();
    char daysOfTheWeek[7][4] = {"Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat"};
    return daysOfTheWeek[now.dayOfTheWeek()];
}


String tomorrow() 
{
    DateTime now = RTC.now();
    char daysOfTheWeek[7][4] = {"Mon", "Tue", "Wed", "Thu", "Fri", "Sat", "Sun"};
    return daysOfTheWeek[now.dayOfTheWeek()];
}


String getValue(String data, int index)
{
    int found = 0;
    int strIndex[] = { 0, -1 };
    int maxIndex = data.length() - 1;

    for (int i = 0; i <= maxIndex && found <= index; i++) {
        if (data.charAt(i) == ',' || i == maxIndex) {
            found++;
            strIndex[0] = strIndex[1] + 1;
            strIndex[1] = (i == maxIndex) ? i + 1 : i;
        }
    }

    return found > index ? data.substring(strIndex[0], strIndex[1]) : "";
}


boolean wifi() 
{
    if (WiFi.isConnected()) {
        Serial.println(WiFi.localIP());
        return true;
    }

    WiFi.mode(WIFI_STA);
    WiFi.begin("WifiGalaxy", "pass123word");

    while(!WiFi.isConnected()) {
        Serial.print('.');
        delay(1000);
    }

    Serial.println(WiFi.localIP());
    return true;
}


void setup() 
{
    Serial.begin(115200);

    lcd.begin(16, 2); 

    RTC.begin();
    RTC.adjust(DateTime(__DATE__, __TIME__));

    dht.begin();

    wifi();
}


void loop() 
{
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("--'C / --%");
    lcd.setCursor(0, 0);
    lcd.print(sensor());
    delay(3000);

    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("   " + date());

    for (int i = 0; i < 10; i++) {
        lcd.setCursor(0, 1);
        lcd.print("    " + time());
        delay(500);
    }

    String weatherString = weather();
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print(today() + " " + getValue(weatherString, 0) + "'C " + getValue(weatherString, 1));
    lcd.setCursor(0, 1);
    lcd.print(tomorrow() + " " + getValue(weatherString, 2) + "'C " + getValue(weatherString, 3));
    delay(5000);
}
```
